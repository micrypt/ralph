
(define-module format
  import: (core)
  export: (<stream>
           <string-stream>
           *standard-out*
           write
           read-char
           peek-char
           advance
           at-end?
           contents))

;; <stream>

(define-protocol <stream>
  (at-end?)
  (peek-char)
  (read-char)
  (write)
  (advance)
  (contents))

;; <string-stream>

(define-type <string-stream>
  (string "")
  (index 0))

(define-function at-end? ((stream <string-stream>))
  (>= (get stream "index")
      (dec (size (get stream "string")))))

(define-function peek-char ((stream <string-stream>))
  (element (get stream "string")
           (get stream "index")))

(define-function read-char ((stream <string-stream>))
  (bind ((char (peek-char stream)))
    (inc! (get stream "index"))
    char))

(define-function write ((stream <string-stream>) object)
  (bind ((string (as-string object))
         (position (get stream "index"))
         (contents (get stream "string"))
         (length (size string)))
    (set! (get stream "string")
          (replace-subsequence contents string
                               start: position
                               end: (+ position length)))
    (advance stream count: length)))

(define-function advance ((stream <string-stream>) #key (count 1))
  (inc! (get stream "index") count))

(define-function contents ((stream <string-stream>))
  (get stream "string"))

(define-function remaining-contents ((stream <string-stream>))
  (copy-sequence (get stream "string")
                 start: (get stream "index")))

;; <file-stream>

(define-type <file-stream>
    file)

(define-function write ((stream <file-stream>) object)
  ((js:get-property (js:get-property stream "file") "write")
   object))

;; <document-stream>

(define-type <document-stream>)

(define-function write ((stream <document-stream>) object)
  ((js:get-property "document" "write")
   object))

(define *standard-out* (select *environment* ==
                         (("commonjs")
                          (make <file-stream>
                                file: (js:get-property (require "system")
                                                       "stdout")))
                         (("browser")
                          (make <document-stream>))))