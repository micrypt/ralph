(define-module ralph/reader
    import: (ralph/core ralph/stream ralph/regexp)
    export: (read))

(define *number-patterns*
    (map (curry make <regexp> ignore-case?: #t pattern:)
         ["^[+-]?0x[0-9a-f]+"
          "^[+-]?0[0-7]+"
          "^[+-]?\\d+\\.?\\d*(?:e-?\\d*(?:\\d\\.?|\\.?\\d)\\d*)?"]))

(define-function make-atom (value)
  (select value ==
    (("#t") (make-boolean #t))
    (("#f") (make-boolean #f))
    (("#key") #key)
    (("#rest") #rest)
    (else:
     (if-bind (number (any? (rcurry match value)
                            *number-patterns*))
       (make-number (first number))
       (if (== (last value) ":")
           (keyword (but-last value))
           (symbol value))))))

(define *whitespace* ["\n" "\r" " " "\t"])
(define *atom-end* (concatenate *whitespace*
                                ["(" ")" "[" "]" "\"" "'" ";"]))

;; TODO:
;; "`" `(%backquote)
;; "," `(%comma)

(define-function read ((stream <stream>))
  (bind ((stack [[]])
         (atom #f)
         (ends []))
    (bind-methods ((add! ()
                     (bind ((value (pop-last stack)))
                       (push-last (last stack) value)))
                   (add-atom! ()
                     (push-last (last stack)
                                (make-atom atom))))
      (while (not (stream-at-end? stream))
        (bind ((char (stream-read-element stream)))
          (if atom
              (if (member? char *atom-end*)
                  (begin
                   (add-atom!)
                   (stream-unread-element stream)
                   (set! atom #f))
                  (set! atom (concatenate atom char)))
              (select (last stack) instance?
                ((<array>)
                 (select char ==
                   ((";")
                    (stream-read-through stream "\n"))
                   (("(")
                    (push-last ends ")")
                    (push-last stack []))
                   (("[")
                    (push-last ends "]")
                    (push-last stack `(js:array)))
                   (((last ends))
                    (pop-last ends)
                    (add!))
                   ((")" "]") (signal (format-to-string "too many closings: %=" char)))
                   (("\"") (push-last stack ""))
                   (else:
                    (unless (member? char *whitespace*)
                      (set! atom char)))))
                ((<string>)
                 (if (== char "\"")
                     (add!)
                     (begin
                      (when (== char "\\")
                        (stream-read-element stream))
                      (set! (last stack)
                            (concatenate (last stack)
                                         char)))))))))
      (if (empty? ends)
          (begin
           (when atom
             (add-atom!))
           (last stack))
          (signal (format-to-string "missing closings: %=" ends))))))
