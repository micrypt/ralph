(define-module ralph/compiler2/alpha
  import: (ralph/format
           ralph/compiler2/environment
           ralph/compiler2/utilities)
  export: (alpha-convert-form))

;; "identifiers" => module name => symbol name => renaming+

(define-function ensure-naming-structure (env module-name name)
  ;; ensure renaming object for env
  (unless (get env "identifiers" module-name)
    (set! (get env "identifiers" module-name)
          (make-object)))
  ;; ensure renaming list for variable
  (bind ((module-renamings (get env "identifiers" module-name)))
    (unless (get module-renamings name)
      (set! (get module-renamings name)
            [])))
  env)

(define $special-symbols '(%all-arguments))

(define-function rename-identifiers (identifiers env)
  (map (method (identifier)
         (if (member? identifier $special-symbols)
             identifier
             (destructuring-bind (name module-name)
                 (destructure-symbol identifier)
               (unless module-name
                 (set! module-name
                       (get env "module" "name")))
               (bind ((identifier*
                       (generate-symbol
                        env (unless (get identifier "generated?")
                              name))))
                 (ensure-naming-structure env module-name name)
                 (push (get env "identifiers" module-name name)
                       identifier*)
                 identifier*))))
       identifiers))

(define-function restore-identifiers (identifiers env)
  (for-each ((identifier identifiers))
            ()
    (unless (member? identifier $special-symbols)
      (destructuring-bind (name module-name)
          (destructure-symbol identifier)
        (unless module-name
          (set! module-name (get env "module" "name")))
        (pop (get env "identifiers" module-name name))))))

(define-function find-identifier (identifier env)
  (if (member? identifier $special-symbols)
      identifier
      (destructuring-bind (name module-name)
          (destructure-symbol identifier)
        (unless module-name
          (set! module-name (get env "module" "name")))
        (bind ((renamings (get env "identifiers"
                               module-name name)))
          (when (and renamings
                     (not (empty? renamings)))
            (first renamings))))))

(define-function alpha-convert-form (exp env)
  (alpha-convert exp env identity))

(define-function alpha-convert (exp env k)
  (select exp instance?
    ((<array>)
     (select (symbol-name (first exp)) ==
       (("%quote" "%native-name")
        (k exp env))
       (("%bind")
        (destructuring-bind (_ (identifier value) form) exp
          ;; convert value, before renaming
          (alpha-convert
           value env
           (method (value* env)
             (destructuring-bind (identifier*)
                 (rename-identifiers [identifier] env)
               (alpha-convert
                form env
                (method (form* env)
                  (restore-identifiers [identifier] env)
                  (k `(%bind (,identifier* ,value*)
                        ,form*)
                     env))))))))
       (("%method")
        (destructuring-bind (_ name parameters form) exp
          (bind ((identifiers (cons name parameters)))
            (destructuring-bind (name* #rest parameters*)
                (rename-identifiers identifiers env)
              (alpha-convert
               form env
               (method (form* env)
                 (restore-identifiers identifiers env)
                 (k `(%method ,name* ,parameters*
                       ,form*)
                    env)))))))
       ;; skip
       (("%set" "%if" "%begin" "%while" "%native"
         "%native-call" "%infix"
         "%object" "%array" "%get-property"
         "%minus" "%plus" "%times" "%divide")
        (destructuring-bind (op #rest arguments) exp
          (alpha-convert*
           arguments env
           (method (arguments* env)
             (k (cons op arguments*)
                env)))))
       (("%define")
        (destructuring-bind (_ identifier value) exp
          ;; convert value, before renaming
          (alpha-convert
           value env
           (method (value* env)
             (bind ((identifier*
                     ;; already renamed ahead of definition?
                     ;; (see <symbol>)
                     (or (find-identifier identifier env)
                         ;; NOTE: not reverted, modifies env!
                         (first (rename-identifiers
                                 [identifier] env)))))
               (k `(%define ,identifier* ,value*)
                  env))))))
       (("%try")
        (destructuring-bind (_ protected-form identifier handling-form) exp
          ;; convert protected-form, before renaming
          (alpha-convert
           protected-form env
           (method (protected-form* env)
             (destructuring-bind (identifier*)
                 (rename-identifiers [identifier] env)
               (alpha-convert
                handling-form env
                (method (handling-form* env)
                  (restore-identifiers [identifier] env)
                  (k `(%try ,protected-form*
                            ,identifier*
                            ,handling-form*)
                     env))))))))
       (else:
        (alpha-convert** exp env k))))
    ((<symbol>)
     (if-bind (identifier (find-identifier exp env))
       (k identifier env)
       (bind ((symbol (resolve-symbol exp env)))
         (if (== (get symbol "module")
                 (get env "module" "name"))
             ;; local, free variable: rename ahead of definition
             ;; (see handling in %define)
             (k (first (rename-identifiers [symbol] env))
                env)
             `(begin
               ;; TODO [#A]: external definition
               ,(k symbol env))))))
    (else:
     (k exp env))))

(define-function alpha-convert* (exp* env k)
  (if (empty? exp*)
      (k [] env)
      (alpha-convert** exp* env k)))

(define-function alpha-convert** (exp env k)
  (alpha-convert
   (first exp) env
   (method (t env)
     (alpha-convert*
      (rest exp) env
      (method (t* env)
        (k (cons t t*)
           env))))))