(define-module ralph/compiler/alpha
  import: (ralph/compiler/utilities ralph/format)
  export: (alpha-convert
           ensure-naming-structure
           rename-identifiers
           restore-identifiers
           find-identifier))

;; "identifiers" => module name => symbol name => renaming+

(define-function ensure-naming-structure (env module-name name)
  ;; ensure renaming object for env
  (unless (get env "identifiers" module-name)
    (set! (get env "identifiers" module-name)
          (make-object)))
  ;; ensure renaming list for variable
  (bind ((module-renamings (get env "identifiers" module-name)))
    (unless (get module-renamings name)
      (set! (get module-renamings name)
            [])))
  env)

(define-function rename-identifiers (identifiers env)
  (map (method (identifier)
         (destructuring-bind (name module-name)
             (destructure-symbol identifier)
           (unless module-name
             (signal (concatenate "NOT QUALIFIED: " (description identifier))))
           (bind ((new-identifier (generate-symbol
                                   env
                                   (unless (get identifier "generated?")
                                     name))))
             (ensure-naming-structure env module-name name)
             (push (get env "identifiers" module-name name)
                   new-identifier)
             new-identifier)))
       identifiers))

(define-function restore-identifiers (identifiers env)
  (for-each ((identifier identifiers))
            ()
    (destructuring-bind (name module-name)
        (destructure-symbol identifier)
      (unless module-name
        (signal (concatenate "NOT QUALIFIED: " (description identifier))))
      (pop (get env "identifiers" module-name name)))))

(define *not-found* (make-object))

(define-function find-identifier (identifier env)
  (destructuring-bind (name module-name)
      (destructure-symbol identifier)
    (unless module-name
      (signal (concatenate "NOT QUALIFIED: " (description identifier))))
    (bind ((renamings (get env "identifiers"
                           module-name name)))
      (if (and renamings
               (not (empty? renamings)))
          (first renamings)
          *not-found*))))

(define-function alpha-convert (form env)
  (select form instance?
    ((<array>)
     (select (symbol-name (first form)) ==
       (("%method")
        (destructuring-bind (_ name arguments body) form
          (bind ((identifiers (cons name arguments))
                 (new-identifiers (rename-identifiers identifiers env))
                 (new-body (alpha-convert body env)))
            (destructuring-bind (new-name #rest new-arguments)
                new-identifiers
              (bind ((result `(%method ,new-name ,new-arguments
                                 ,new-body)))
                (restore-identifiers identifiers env)
                result)))))
       (("%bind")
        (destructuring-bind (_ (var value) body) form
          (bind ((new-value (alpha-convert value env)))
            (destructuring-bind (new-var)
                (rename-identifiers [var] env)
              (bind ((result
                      `(%bind (,new-var ,new-value)
                         ,(alpha-convert body env))))
                (restore-identifiers [var] env)
                result)))))
       (("%try")
        (destructuring-bind (_ body var catch) form
          (bind ((new-body (alpha-convert body env)))
            (destructuring-bind (new-var)
                (rename-identifiers [var] env)
              (bind ((result
                      `(%try ,new-body ,new-var
                             ,(alpha-convert catch env))))
                (restore-identifiers [var] env)
                result)))))
       (("%define")
        ;; rename in future expression
        ;; NOTE: modifies env!
        (destructuring-bind (_ identifier) form
          (bind ((name (symbol-name identifier)))
            (destructuring-bind (new-identifier)
                (rename-identifiers [identifier] env)
              ;; track new identifier -> old identifier
              (set! (get env "original-identifier"
                         (symbol-name new-identifier))
                    identifier)
              `(%define ,new-identifier)))))
       ;; skip
       (("%if" "%while" "%set" "%begin" "%get-property" "%object"
         "%array" "%native" "%native-call" "%infix"
         "%minus" "%plus" "%times" "%divide")
        (cons (first form)
              (map (rcurry alpha-convert env)
                   (rest form))))
       ;; none
       (("%native-name" "%symbol") form)
       (else:
        (map (rcurry alpha-convert env)
             form))))
    ((<symbol>)
     (bind ((value (find-identifier form env)))
       (if (== value *not-found*)
           form
           value)))
    (else: form)))